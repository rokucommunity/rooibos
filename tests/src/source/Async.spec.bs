namespace tests
  @async(1000)
  @SGNode("NodeExample")
  @suite
  class AsyncNodeTests extends rooibos.BaseTestSuite

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    @describe("NodeExample tests")
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    @beforeEach
    function beforeEachGroup()
      m.cleanStubs()
    end function

    @async
    @it("normal async test")
    function _()
      callbackFunction = sub()
        m.wasCalled = true
        m.testSuite.assertTrue(true)
        m.testSuite.done()
        ? "done one"
      end sub

      delayCall(0.02, callbackFunction)
    end function

    @async
    @it("async with stub call for source function")
    function _()
      getGlobalAA().wasCalled = false
      m.stubCall(globalFunctionWithoutReturn, function()
        m.wasCalled = true
        m.testSuite.assertTrue(true)
        m.testSuite.done()
        ? "done two"
      end function)

      delayCall(0.02, globalFunctionWithoutReturn)
    end function

    @async
    @it("async with stub call for node function")
    function _()
      m.stubCall(SetLabelText, function(text)
        m.wasCalled = true
        m.testSuite.assertEqual(text, "")
        m.testSuite.assertTrue(true)
        m.testSuite.done()
        ? "done three"
      end function)

      delayCall(0.02, SetLabelText)
    end function
  end class
end namespace
