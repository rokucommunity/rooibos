import "pkg:/source/roku_modules/rooibos/BaseTestSuite.brs"

'@TestSuite
namespace AssertionTests

  class Basic
    public name = "Basic tests"

    override function beforeEach()

    end function 

    override function afterEach()

    end function 

    '@Test some long english text here
    '@Params[true, true]
    '@Params[false, false]
    '@Params[invalid, false]
    '@Params[0, false]
    '@Params[1, false]
    '@Params["test", false]
    function assertTrue(value, expectedAssertResult) as void

      assertResult = m.assertTrue(value)
      isFail = m.currentResult.isFail
      m.currentResult.Reset()

      m.assertEqual(assertResult, expectedAssertResult, token.start.line)
      m.assertEqual(isFail, not expectedAssertResult)
    end function
  end class

  class BasicAssertions

    '@Test Fail
    function fail() as void

      assertResult = m.Fail("reason")
      isFail = m.currentResult.isFail
      m.currentResult.Reset()

      m.assertFalse(assertResult)
      m.assertTrue(isFail)
    end function

    '@Test AssertTrue
    '@Params[true, true]
    '@Params[false, false]
    '@Params[invalid, false]
    '@Params[0, false]
    '@Params[1, false]
    '@Params["test", false]
    function assertTrue(value, expectedAssertResult) as void

      assertResult = m.assertTrue(value)
      isFail = m.currentResult.isFail
      m.currentResult.Reset()

      m.assertEqual(assertResult, expectedAssertResult)
      m.assertEqual(isFail, not expectedAssertResult)
    end function

    '@Test AssertFalse
    '@Params[false, true]
    '@Params[true, false]
    '@Params[invalid, false]
    '@Params[0, false]
    '@Params[1, false]
    '@Params["test", false]
    function assertFalse(value, expectedAssertResult) as void

      assertResult = m.assertFalse(value)

      isFail = m.currentResult.isFail
      m.currentResult.Reset()

      m.assertEqual(assertResult, expectedAssertResult)
      m.assertEqual(isFail, not expectedAssertResult)
    end function
  end class

  class AssertArrayContainsAAs

    '@Test Fail
    '@Params[[{"one":1}], [{"one":2}]]
    '@Params[[{"one":1}], [{"one":"a"}]]
    '@Params[[{"one":1}], [{}]]
    '@Params[[{"one":1}], [invalid]]
    '@Params[[{"one":1}], []]
    '@Params[[{"one":1}], invalid]
    '@Params[[{"one":1}], [[]] ]
    '@Params[[{"one":1}], ["wrong"] ]
    '@Params[[{"one":1}], [2] ]
    '@Params[[{"one":"a"}], [{"one":1}] ]
    '@Params[[{"two":1}], [{"one":1}] ]
    '@Params[[invalid], [{"one":1}] ]
    '@Params[invalid, [{"one":1}] ]
    '@Params[[{"one":1, "two":2}], [{"one":"1"}] ]
    '@Params[[{"one":1}, {"two":2}], [{"one":"1"}, {"two":"a"}] ]
    '@Params[[{"one":1}, {"two":2}], [{"a":1}, {"a":1}, {"a":1}] ]
    function assertArrayContainsAAs_Fail(expectedAAs, items) as void

      assertResult = m.assertArrayContainsAAs(items, expectedAAs)

      isFail = m.currentResult.isFail
      m.currentResult.Reset()

      m.assertFalse(assertResult)
      m.assertTrue(isFail)
    end function


    '@Test pass
    '@Params[[], []]
    '@Params[[{}], [{}]]
    '@Params[[{"one":1}], [{"one":1}]]
    '@Params[[{"one":1, "two":2}], [{"one":1, "two":2}]]
    '@Params[[{"one":1, "two":2}], [{ "two":2, "one":1}]]
    '@Params[[{"one":1, "two":2}, {"one":1}], [{"one":1}, { "two":2, "one":1}]]
    '@Params[[{"one":1, "two":2}, {"one":1}, {"three":3}], [{"one":1}, {"three":3}, { "two":2, "one":1}]]
    function assertArrayContainsAAs_Pass(expectedAAs, items) as void

      assertResult = m.assertArrayContainsAAs(items, expectedAAs)

      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertTrue(assertResult)
      m.assertFalse(isFail)
    end function
  end class

  class AssertGlobal

    override function beforeEach() as void
      m.beforeEachGlobal = m.global
    end function

    override function afterEach() as void
      m.afterEachGlobal = m.global
    end function

    '@Test global is in test
    function isPassedIntoTest() as void
      m.assertNotInvalid(m.global)
    end function

    '@Test global is in before each and after each
    function isPassedInto_beforeEach_and_afterEach() as void
      m.assertNotInvalid(m.global)
      m.assertNotInvalid(m.beforeEachGlobal)
      m.assertNotInvalid(m.afterEachGlobal)
    end function
  end class

  class AssertArrayContainsOnlyValuesOfType
    '@Test pass
    '@Params[["one", "two", "three"], "String"]
    '@Params[[1, 2, 3], "Integer"]
    '@Params[[true, true, false], "Boolean"]
    '@Params[[[true, true], [false, false]], "Array"]
    '@Params[[{"test":1}, {"test":1}], "AssociativeArray"]
    function assertArrayContainsOnlyValuesOfType_Pass(values, typeName) as void

      assertResult = m.assertArrayContainsOnlyValuesOfType(values, typeName)
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertTrue(assertResult)
      m.assertFalse(isFail)

    end function

    '@Test fail
    '@Params[["one", 2, "three"], "String"]
    '@Params[[1, "two", 3], "Integer"]
    '@Params[[true, "true", false], "Boolean"]
    '@Params[[[true, true], false, false], "Array"]
    '@Params[[{"test":1}, "notAA"], "AssociativeArray"]
    '@Params[["one", "two", "three"], "UnknownType"]
    '@Params[["one", "two", "three"], "Integer"]
    '@Params[[1, 2, 3], "String"]
    '@Params[[true, true, false], "String"]
    '@Params[[[true, true], [false, false]], "AssociativeArray"]
    '@Params[[{"test":1}, {"test":1}], "Array"]
    function assertArrayContainsOnlyValuesOfType_Fail(values, typeName) as void

      assertResult = m.assertArrayContainsOnlyValuesOfType(values, typeName)
      isFail = m.currentResult.isFail

      isFail = m.currentResult.isFail
      m.currentResult.Reset()

      m.assertFalse(assertResult)
      m.assertTrue(isFail)


    end function
  end class

  class TestWhiteSpacesWorkWithAnnotations

    'some comments to
    'demonstrate
    '@Test comments between tests
    'that we can have comments
    'between functions and tags
    function betweenTests() as void
      m.assertTrue(true)
    end function

    'some comments to
    'demonstrate
    '@Test comments between tests with params
    '@Params[1]

    '@Params[2]
    'that we can have comments
    '@Params[3]
    'between functions and tags
    '@Params[4]

    function params(value) as void
      m.assertTrue(true)
    end function
  end class

  class AssertNotEmpty

    '@Test pass
    '@Params[["one", "two", "three"]]
    '@Params[[1, 2, 3]]
    '@Params[[true]]
    '@Params[[[true, true], [false, false]]]
    '@Params[[{"test":1}]]
    '@Params["not empty"]
    '@Params[[invalid]]
    function assertNotEmpty_Pass(values) as void

      assertResult = m.assertNotEmpty(values)
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertTrue(assertResult)
      m.assertFalse(isFail)

    end function

    '@Test fail
    '@Params[invalid]
    '@Params[[]]
    '@Params[{}]
    '@Params[1]
    '@Params[""]
    function assertNotEmpty_Fail(values) as void

      assertResult = m.assertNotEmpty(values)
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(assertResult)
      m.assertTrue(isFail)

    end function

  end class

  class AssertEmpty

    '@Test pass
    '@Params[[]]
    '@Params[{}]
    '@Params[""]
    function assertEmpty_Pass(values) as void

      assertResult = m.assertEmpty(values)
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertTrue(assertResult)
      m.assertFalse(isFail)

    end function

    '@Test fail
    '@Params[1]
    '@Params[invalid]
    '@Params[["one", "two", "three"]]
    '@Params[[1, 2, 3]]
    '@Params[[true]]
    '@Params[[[true, true], [false, false]]]
    '@Params[[{"test":1}]]
    '@Params["not empty"]
    '@Params[[invalid]]
    function assertEmpty_Fail(values) as void

      assertResult = m.assertEmpty(values)
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(assertResult)
      m.assertTrue(isFail)

    end function

  end class

  class Expect

    '@Test multi return values
    function expect_multiValues()
      obj = {}
      m.expect(obj, "mockMethod", 5, invalid, { "multiResult": ["one", 2, invalid, "last"] }, true)

      result = obj.mockMethod()
      m.assertEqual(result, "one")

      result = obj.mockMethod()
      m.assertEqual(result, 2)

      result = obj.mockMethod()
      m.assertEqual(result, invalid)

      result = obj.mockMethod()
      m.assertEqual(result, "last")

      result = obj.mockMethod()
      m.assertEqual(result, "last")

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)

    end function
  end class

  class ExpectOnceOverloaded

    '@Test simple test
    function expect_multiExpect_success()
      obj = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "mockMethod", [arg1], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2], result2, true)
      m.expectOnce(obj, "mockMethod", [arg3], result3, true)

      result = obj.mockMethod(arg1)
      m.assertEqual(result, result1)

      result = obj.mockMethod(arg2)
      m.assertEqual(result, result2)

      result = obj.mockMethod(arg3)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)
    end function


    '@Test can set up multi expects on same method - one invocation with any args
    function expect_multiExpect_success_oneCallsArgsNotTracked()
      obj = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "mockMethod", [arg1], result1, true)
      m.expectOnce(obj, "mockMethod", invalid, result2, true)
      m.expectOnce(obj, "mockMethod", [arg3], result3, true)

      result = obj.mockMethod(arg1)
      m.assertEqual(result, result1)

      result = obj.mockMethod("do not care about args", "used in invocation", 2)
      m.assertEqual(result, result2)

      result = obj.mockMethod(arg3)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)

    end function

    '@Test can set up multi expects on same method - multi params
    function expect_multiExpect_multi_args_success()
      obj = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "mockMethod", [arg1, arg2, arg3], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2, arg3, arg1], result2, true)
      m.expectOnce(obj, "mockMethod", [arg3, arg2, arg1], result3, true)

      result = obj.mockMethod(arg1, arg2, arg3)
      m.assertEqual(result, result1)

      result = obj.mockMethod(arg2, arg3, arg1)
      m.assertEqual(result, result2)

      result = obj.mockMethod(arg3, arg2, arg1)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)

    end function

    '@Test multi test, multi params with other expects
    function expect_multiExpect_multi_args_others_success()
      obj = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "anotherMockMethod", invalid, "another", true)
      m.expectOnce(obj, "anotherMockMethod2", [1, 2, 3], "another2", true)
      m.expectOnce(obj, "mockMethod", [arg1, arg2, arg3], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2, arg3, arg1], result2, true)
      m.expectOnce(obj, "mockMethod", [arg3, arg2, arg1], result3, true)

      result = obj.anotherMockMethod()
      m.assertEqual(result, "another")

      result = obj.anotherMockMethod2(1, 2, 3)
      m.assertEqual(result, "another2")

      result = obj.mockMethod(arg1, arg2, arg3)
      m.assertEqual(result, result)

      result = obj.mockMethod(arg2, arg3, arg1)
      m.assertEqual(result, result2)

      result = obj.mockMethod(arg3, arg2, arg1)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)

    end function

    '@Test multi test, multi params with other expects - fail others
    function expect_multiExpect_multi_args_others_fail()
      obj = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "anotherMockMethod", ["not passed"], "another", true)
      m.expectOnce(obj, "anotherMockMethod2", [1, 2, 3], "another2", true)
      m.expectOnce(obj, "mockMethod", [arg1, arg2, arg3], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2, arg3, arg1], result2, true)
      m.expectOnce(obj, "mockMethod", [arg3, arg2, arg1], result3, true)

      result = obj.anotherMockMethod()
      m.assertEqual(result, "another")

      result = obj.anotherMockMethod2(1, 2, 3)
      m.assertEqual(result, "another2")

      result = obj.mockMethod(arg1, arg2, arg3)
      m.assertEqual(result, result)

      result = obj.mockMethod(arg2, arg3, arg1)
      m.assertEqual(result, result2)

      result = obj.mockMethod(arg3, arg2, arg1)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertTrue(isFail)

    end function

    '@Test can set up multi expects on same method
    '@Params["arg1_", "arg2", "arg3"]
    '@Params["arg1", "arg2", "arg3_"]
    '@Params["arg1", "arg2_", "arg3"]
    '@Params["arg1", "arg2_", "arg3"]
    '@Params["arg1_", "arg2_", "arg3"]
    '@Params["arg1_", invalid, "arg3"]
    function expect_multiExpect_fail(call1, call2, call3)
      obj = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "mockMethod", [arg1], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2], result2, true)
      m.expectOnce(obj, "mockMethod", [arg3], result3, true)

      result = obj.mockMethod(call1)
      m.assertEqual(result, result1)

      result = obj.mockMethod(call2)
      m.assertEqual(result, result2)

      result = obj.mockMethod(call2)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertTrue(isFail)
    end function
  end class

  class ExpectOnceOverLoaded_DifferentObjects

    '@Test success
    function expect_multiExpect_differentOnj_success()
      obj = {}
      obj2 = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "mockMethod", [arg1], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2], result2, true)
      m.expectOnce(obj2, "mockMethod", [arg3], result3, true)

      result = obj.mockMethod(arg1)
      m.assertEqual(result, result1)

      result = obj.mockMethod(arg2)
      m.assertEqual(result, result2)

      result = obj2.mockMethod(arg3)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)

    end function

    '@Test fail to match
    function expect_multiExpect_differentOnj_fail()
      obj = {}
      obj2 = {}
      arg1 = "arg1"
      arg2 = "arg2"
      arg3 = "arg3"
      result1 = 1
      result2 = 2
      result3 = 3

      m.expectOnce(obj, "mockMethod", [arg1], result1, true)
      m.expectOnce(obj, "mockMethod", [arg2], result2, true)
      m.expectOnce(obj2, "mockMethod", [arg3], result3, true)

      result = obj.mockMethod(arg1)
      m.assertEqual(result, result1)

      result = obj.mockMethod(arg2)
      m.assertEqual(result, result2)

      result = obj2.mockMethod(arg3)
      m.assertEqual(result, result3)

      m.assertMocks()
      isFail = m.currentResult.isFail

      m.currentResult.Reset()
      m.assertFalse(isFail)

    end function
  end class

  'ASSERTIONS TO WRITE TESTS FOR!

  'This is coming soon!

  '    AssertEqual
  '    AssertLike
  '    AssertNotEqual
  '    AssertInvalid
  '    AssertNotInvalid
  '    AssertAAHasKey
  '    AssertAANotHasKey
  '    AssertAAHasKeys
  '    AssertAANotHasKeys
  '    AssertArrayNotContains
  '    AssertArrayContainsSubset
  '    AssertArrayNotContainsSubsetet
  '    AssertArrayCount
  '    AssertArrayNotCount
  '    AssertArrayContainsOnly
  '    AssertType
  '    AssertSubType
  '
  '    'Node extensions
  '    AssertNodeCount
  '    AssertNodeNotCount
  '    AssertNodeEmpty
  '    AssertNodeNotEmpty
  '    AssertNodeContains
  '    AssertNodeNotContains
  '    AssertNodeContainsFields
  '    AssertNodeNotContainsFields

  '    AssertArray
  '    AssertAAContainsSubset
  '
  '    'Mocking and stubbing
  '    AssertMocks
  '    MockFail

end namespace