namespace tests
  @SGNode("NodeExample")
  @suite
  class NodePromisesTests extends rooibos.BaseTestSuite

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("rooibos.promises.chain()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async(100)
		@it("async test that times out")
		sub _()
		end sub

		@async
		@it("example using rooibos.promise.chain")
		sub _()
			context = {
				thenCount: 0
				catchCount: 0
				finallyCount: 0
			}

			rooibos.promises.chain(rooibos.promises.resolve(1), context).then(sub(result, context)
				context.thenCount++
				m.testSuite.assertEqual(result, 1)
			end sub).catch(sub(error, context)
				context.catchCount++
				m.testSuite.fail("should not get here")
			end sub).finally(sub(context)
				context.finallyCount++
				m.testSuite.assertEqual(context, {
					thenCount: 1
					catchCount: 0
					finallyCount: 1
				})
				m.testSuite.done()
			end sub)
		end sub

		@it("returning a promise that will resolve to the test")
		function _()
			return toPromiseWithDelay(0.1, true, true)
		end function

		@it("returning a promise that will reject to the test")
		function _()
			try
				v = 1 / 0
			catch error
				return toPromiseWithDelay(0.1, error, false)
			end try
		end function
  end class
end namespace

function toPromiseWithDelay(duration = 0.0001 as float, value = true as dynamic, resolve = true as boolean) as dynamic
	differed = rooibos.promises.create()
	rooibos.promises.internal.delay(sub(context as dynamic)
		if context.resolve then
			rooibos.promises.resolve(context.value, context.differed)
		else
			rooibos.promises.reject(context.value, context.differed)
		end if
	end sub, {differed: differed, value: value, resolve: resolve}, duration)
	return differed
end function